# (C) Copyright 1988- ECMWF.
#
# This software is licensed under the terms of the Apache Licence Version 2.0
# which can be obtained at http://www.apache.org/licenses/LICENSE-2.0.
# In applying this licence, ECMWF does not waive the privileges and immunities
# granted to it by virtue of its status as an intergovernmental organisation
# nor does it submit to any jurisdiction.

# Define the naive GPU dwarf variant as an ECBuild feature
ecbuild_add_option( FEATURE CLOUDSC_GPU_KERNELS
    DESCRIPTION "Build naive GPU version of CLOUDSC using `!$acc kernels`" DEFAULT OFF
    CONDITION Serialbox_FOUND OR HDF5_FOUND
)

# Define the CLAW-based GPU dwarf variant as an ECBuild feature
ecbuild_add_option( FEATURE CLOUDSC_GPU_CLAW
    DESCRIPTION "Build optimized GPU version of CLOUDSC derived from CLAW" DEFAULT OFF
    CONDITION Serialbox_FOUND OR HDF5_FOUND
)

ecbuild_add_option( FEATURE CLOUDSC_GPU_SCC
    DESCRIPTION "Build optimized GPU version of CLOUDSC using SCC layout" DEFAULT OFF
    CONDITION Serialbox_FOUND OR HDF5_FOUND
)

ecbuild_add_option( FEATURE CLOUDSC_GPU_SCC_HOIST
    DESCRIPTION "Build optimized GPU version of CLOUDSC using SCC with hoisted temporary arrays" DEFAULT OFF
    CONDITION Serialbox_FOUND OR HDF5_FOUND
)


if( HAVE_CLOUDSC_GPU_CLAW )
  ecbuild_add_executable(
      TARGET dwarf-cloudsc-gpu-claw
      SOURCES
          dwarf_cloudsc_gpu.F90
          cloudsc_driver_gpu_claw_mod.F90
          cloudsc.claw.gpu.F90
      DEFINITIONS ${CLOUDSC_DEFINITIONS} CLOUDSC_GPU_CLAW
  )
  if( HAVE_ACC AND TARGET OpenACC::OpenACC_Fortran )
      target_link_libraries( dwarf-cloudsc-gpu-claw PRIVATE OpenACC::OpenACC_Fortran )
      target_link_libraries( dwarf-cloudsc-gpu-claw PRIVATE cloudsc-common-lib-static )
  else()
      target_link_libraries( dwarf-cloudsc-gpu-claw PRIVATE cloudsc-common-lib )
  endif()

  if( HAVE_MPI AND TARGET MPI::MPI_Fortran )
      target_link_libraries( dwarf-cloudsc-gpu-claw PRIVATE MPI::MPI_Fortran )
  endif()
  if ( HAVE_HDF5 )
      target_include_directories( dwarf-cloudsc-gpu-claw PRIVATE ${HDF5_Fortran_INCLUDE_DIRS} )
  endif()
endif()


if( HAVE_CLOUDSC_GPU_SCC )
  add_custom_target(dwarf-cloudsc-gpu-scc_all)
  foreach(X RANGE 1 5)
    ecbuild_add_executable(
        TARGET dwarf-cloudsc-gpu-scc-${X}
        SOURCES
            dwarf_cloudsc_gpu.F90
            cloudsc_driver_gpu_scc_mod.F90
            cloudsc_gpu_scc_mod.F90
        DEFINITIONS ${CLOUDSC_DEFINITIONS} CLOUDSC_GPU_SCC
    )
    target_compile_options( dwarf-cloudsc-gpu-scc-${X} PUBLIC -cuda)
    target_link_options( dwarf-cloudsc-gpu-scc-${X} PUBLIC -cuda)
    if( HAVE_ACC AND TARGET OpenACC::OpenACC_Fortran )
        target_link_libraries( dwarf-cloudsc-gpu-scc-${X} PRIVATE OpenACC::OpenACC_Fortran )
        target_link_libraries( dwarf-cloudsc-gpu-scc-${X} PRIVATE cloudsc-common-lib-static )
    else()
        target_link_libraries( dwarf-cloudsc-gpu-scc-${X} PRIVATE cloudsc-common-lib )
    endif()

    if( HAVE_MPI AND TARGET MPI::MPI_Fortran )
        target_link_libraries( dwarf-cloudsc-gpu-scc-${X} PRIVATE MPI::MPI_Fortran )
    endif()
    if ( HAVE_HDF5 )
        target_include_directories( dwarf-cloudsc-gpu-scc-${X} PRIVATE ${HDF5_Fortran_INCLUDE_DIRS} )
    endif()
    target_link_libraries(dwarf-cloudsc-gpu-scc-${X} PRIVATE config_${X})
    add_dependencies(dwarf-cloudsc-gpu-scc_all dwarf-cloudsc-gpu-scc-${X})
  endforeach()
endif()


if( HAVE_CLOUDSC_GPU_SCC_HOIST )
  add_custom_target(dwarf-cloudsc-gpu-scc-hoist_all)
  foreach(X RANGE 1 5)
    ecbuild_add_executable(
        TARGET dwarf-cloudsc-gpu-scc-hoist-${X}
        SOURCES
            dwarf_cloudsc_gpu.F90
            cloudsc_driver_gpu_scc_hoist_mod.F90
            cloudsc_gpu_scc_hoist_mod.F90
        DEFINITIONS ${CLOUDSC_DEFINITIONS} CLOUDSC_GPU_SCC_HOIST
    )
    target_compile_options( dwarf-cloudsc-gpu-scc-hoist-${X} PUBLIC -cuda)
    target_link_options( dwarf-cloudsc-gpu-scc-hoist-${X} PUBLIC -cuda)
    if( HAVE_ACC AND TARGET OpenACC::OpenACC_Fortran )
        target_link_libraries( dwarf-cloudsc-gpu-scc-hoist-${X} PRIVATE OpenACC::OpenACC_Fortran )
        target_link_libraries( dwarf-cloudsc-gpu-scc-hoist-${X} PRIVATE cloudsc-common-lib-static )
    else()
        target_link_libraries( dwarf-cloudsc-gpu-scc-hoist-${X} PRIVATE cloudsc-common-lib )
    endif()

    if( HAVE_MPI AND TARGET MPI::MPI_Fortran )
        target_link_libraries( dwarf-cloudsc-gpu-scc-hoist-${X} PRIVATE MPI::MPI_Fortran )
    endif()
    if ( HAVE_HDF5 )
        target_include_directories( dwarf-cloudsc-gpu-scc-hoist-${X} PRIVATE ${HDF5_Fortran_INCLUDE_DIRS} )
    endif()
    target_link_libraries(dwarf-cloudsc-gpu-scc-hoist-${X} PRIVATE config_${X})
    add_dependencies(dwarf-cloudsc-gpu-scc-hoist_all dwarf-cloudsc-gpu-scc-hoist-${X})
  endforeach()
endif()


# Create symlink for the input data
if( HAVE_SERIALBOX )
  execute_process(COMMAND ${CMAKE_COMMAND} -E create_symlink
      ${CMAKE_CURRENT_SOURCE_DIR}/../../data ${CMAKE_CURRENT_BINARY_DIR}/../../../data )
endif()

if( HAVE_HDF5 )
  execute_process(COMMAND ${CMAKE_COMMAND} -E create_symlink
      ${CMAKE_CURRENT_SOURCE_DIR}/../../config-files/input.h5 ${CMAKE_CURRENT_BINARY_DIR}/../../../input.h5 )
  execute_process(COMMAND ${CMAKE_COMMAND} -E create_symlink
      ${CMAKE_CURRENT_SOURCE_DIR}/../../config-files/reference.h5 ${CMAKE_CURRENT_BINARY_DIR}/../../../reference.h5 )
endif()
